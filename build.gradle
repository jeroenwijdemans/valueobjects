group = 'com.wijdemans'
description = 'Simple value objects library that can be used to replace Strings, Integers and the like with real objects.'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:1.4.5'
    }
}

apply plugin: "org.owasp.dependencycheck"
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'groovy'
apply plugin: "jacoco"

task version {
    doFirst {
        println project.version
    }
}
jacoco {
    reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.destination "${buildDir}/jacocoHtml"
    }
}

test { finalizedBy jacocoTestReport }

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencyCheck {
    autoUpdate = false
    cveValidForHours = 4
    failBuildOnCVSS = 1
    format = "XML"
}

ext {
    libraries = [:]
    versions = [:]
}

versions += [
        slf4j : '1.7.18',
        spock : '1.0-groovy-2.4',
        groovy: '2.4.6'
]

libraries.logging = [
        "org.slf4j:log4j-over-slf4j:$versions.slf4j",
]

libraries.testframework = [
        "org.spockframework:spock-core:$versions.spock",
        "org.codehaus.groovy:groovy:$versions.groovy"
]

dependencies {
    compile libraries.logging
    testCompile libraries.testframework
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getenv("OSSRH_JIRA_USERNAME"), password: System.getenv("OSSRH_JIRA_PASSWORD"))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.getenv("OSSRH_JIRA_USERNAME"), password: System.getenv("OSSRH_JIRA_PASSWORD"))
            }

            pom.project {
                name project.name
                packaging 'jar'
                description project.description
                url 'https://github.com/jeroenwijdemans/valueobjects.git'

                scm {
                    connection 'scm:https://github.com/jeroenwijdemans/valueobjects.git'
                    developerConnection 'scm:git@github.com:jeroenwijdemans/valueobjects.git'
                    url 'https://github.com/jeroenwijdemans/valueobjects.git'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'jeroenwijdemans'
                        name 'Jeroen Wijdeans'
                        email 'jeroenwijdemans@gmail.com'
                    }
                }
            }
        }
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        def id = System.getenv("GPG_KEY_NAME")
        def password = System.getenv("GPG_PASSPHRASE")
        def ring = System.properties['user.home'] + "/.gnupg/secring.gpg"

        allprojects { ext."signing.keyId" = id }
        allprojects { ext."signing.password" = password }
        allprojects { ext."signing.secretKeyRingFile" = ring }
    }
}